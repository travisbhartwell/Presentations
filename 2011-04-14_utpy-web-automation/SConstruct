import os
import sys

SLIDES = "presentation"
pdf_file, rst_file, tex_file = \
    ["%s.%s" % (SLIDES, ext) for ext in ["pdf", "rst", "tex"]]

# TODO: Split out custom builders and commands into custom file
# TODO: Abstract this out, adding parameters for theme and color theme,
#       or at least extra command line options
RST2BEAMER_COMMAND = "rst2beamer --codeblocks-use-pygments"
rst2beamer_builder = \
    Builder(action="%s $SOURCE $TARGET" % RST2BEAMER_COMMAND,
            suffix=".tex",
            src_suffix=".rst")

env = Environment(ENV=os.environ)
env.Append(BUILDERS = {"rst2beamer": rst2beamer_builder})

beamer_output = env.rst2beamer(SLIDES)
pdf_output = env.PDF(target=pdf_file, source=tex_file)
env.NoClean(pdf_output)
Default(pdf_output)

PLATFORM_OPEN_COMMANDS = {"win32": "start.exe",
                          "darwin": "open",
                          "linux2": "xdg-open"}
LAUNCH_COMMAND = "%s $SOURCE" % PLATFORM_OPEN_COMMANDS[sys.platform]

view = env.Alias("view", [pdf_file], LAUNCH_COMMAND)
env.AlwaysBuild(view)
